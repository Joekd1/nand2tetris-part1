// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    Mux8Way16(
        a=currentVal,
        b=incOut,
        c=in,
        d=in,
        e=false,
        f=false,
        g=false,
        h=false,
        sel[0]=inc,
        sel[1]=load,
        sel[2]=reset,
        out=regIn
    );
    
    Inc16(in=currentVal, out=incOut);
    Register(in=regIn, load=true, out=currentVal, out=out);
}
